#!/bin/zsh

# ======================================= #
#                  _                      #
#          _______| |__  _ __ ___         #
#         |_  / __| '_ \| '__/ __|        #
#        _ / /\__ \ | | | | | (__         #
#       (_)___|___/_| |_|_|  \___|        #
#          ZSH Configuration File         #
# ======================================= #

# ==================== INTERACTIVITY CHECK ====================

if [[ $- != *i* ]]; then
  return
fi

# ==================== CORE SETTINGS & VARIABLES ====================

SHELL_CONFIG="$HOME/dotfiles/zsh.pkd"

# Add error to ERROR_STATUS with automatic filename and line number
function config_err() {
  local message="$1"
  local caller_info="${funcfiletrace[1]##*/}"
  ERROR_STATUS="$ERROR_STATUS\n\t$message ($caller_info)"
}

function config_source() {
  if ! [ -f "$SHELL_CONFIG/$1.zsh" ]; then
    config_err "Could not source: '$1.zsh'"
  else
    source "$SHELL_CONFIG/$1.zsh"
  fi
}

config_source settings

# ==================== OPERATING SYSTEM DETECTION ====================

case "$OSTYPE" in
  linux*)
    OPERATING_SYSTEM="linux"
    BREW_PATH="/home/linuxbrew/.linuxbrew"
    config_source linux
    HOSTNAME=$(hostname | \grep -E -o '^[^.]+')
    ;;
  darwin*)
    OPERATING_SYSTEM="macos"
    BREW_PATH="/opt/homebrew"
    config_source macos
    HOSTNAME=$(scutil --get ComputerName)
    ;;
  *)
    OPERATING_SYSTEM=""
    HOSTNAME="HOSTNAME NOT FOUND"
    config_err "OS '($OSTYPE)' not supported"
    ;;
esac

# ==================== TIMEKEEPING ====================

START_TIME=$(gdate +%s%3N)

# ==================== SOURCING ====================

local ZSH_MODULES=(
  "environment"
  "zinit"
  "zsh"
  "path"
  "zoxide"
  "colorize"
  "fzf"
  "functions"
  "grc"
  "aliases"
)

for module in $ZSH_MODULES[@]; do
  config_source $module
done

# ==================== IMPORT FROM PACKAGES ====================

local DOTFILES_DIR="$HOME/dotfiles"

for package in $DOTFILES_DIR/*.pkd; do
  if [ -f "$package/init.zsh" ]; then
    source "$package/init.zsh"
  fi
done

if [ -f "$DOTFILES_DIR/scripts/init.zsh" ]; then
  source "$DOTFILES_DIR/scripts/init.zsh"
fi

# ==================== ERROR CHECK ====================

if [[ "$ERROR_STATUS" != "" ]]; then
  DO_SPLASH_SCREEN=0
  local MESSAGE_LENGTH=35
  echo -ne "\n\n\033[91mErrors During Zsh Initialization\033[90m "
  for _ in {$MESSAGE_LENGTH..$COLUMNS}; do
    echo -n "="
  done
  echo -e "\033[0m"
  echo -e "$ERROR_STATUS"
  echo -e " \033[90m"
  for _ in {2..$COLUMNS}; do
    echo -n "="
  done
  echo "\033[0m\n"
fi

# ==================== SPLASH SCREEN ====================

if (( DO_SPLASH_SCREEN )); then
  config_source splash_screen
fi

# ==== DISPLAY LOAD TIME ====

echo "\033[90mload time: $(( $(gdate +%s%3N) - START_TIME ))ms\033[0m"

# ==================== PROMPT & CURSOR ====================

config_source prompt
